load("baseIntegrationTest.stlx");
registerTestSuite("core_quantifiers");

// ************************************************************************** \\

forallTests := procedure() {
    a := b := c := d := "test";
    // none fails
    e := forall(a in [1 .. 5], b in [1 .. 5] | a * b <= 25);
    // first fail: c == 4, d == 4
    f := forall(c in [1 .. 5], d in [1 .. 5] | (c * d) < 16);

    validateForTestCase("forall")
        .that(
            a == om && b == om && e && c == 4 && d == 4 && !f
        ).is(
            true
        );
};
forallTests();

existsTests := procedure() {
    a := b := c := d := "test";
    // first exists: a == 4, b == 4
    e := exists(a in [1 .. 5], b in [1 .. 5] | a * b >= 16);
    // nothing exists
    f := exists(c in [1 .. 5], d in [1 .. 5] | c * d > 25);

    validateForTestCase("exists")
        .that(
            a == 4 && b == 4 && e && c == om && d == om && !f
        ).is(
            true
        );
};
existsTests();

recurringVariable := procedure() {
    isRightUnique := procedure(r) {
        return forall([x,y1] in r, [x,y2] in r | y1 == y2);
    };

    validateForTestCase("recurringVariable: isRightUnique 1")
        .that(
            isRightUnique({[1, 1], [2, 2]})
        ).is(
            true
        );
    validateForTestCase("recurringVariable: isRightUnique 2")
        .that(
            isRightUnique({[1, 1], [1, 2]})
        ).is(
            false
        );
};
recurringVariable();

nestedForall := procedure() {
    isRightUniqueClumsy := procedure(r) {
        return forall([x1,y1] in r | forall([x2,y2] in r | x1 == x2 => y1 == y2));
    };

    validateForTestCase("nestedForall: isRightUniqueClumsy 1")
        .that(
            isRightUniqueClumsy({[1, 1], [2, 2]})
        ).is(
            true
        );
    validateForTestCase("nestedForall: isRightUniqueClumsy 2")
        .that(
            isRightUniqueClumsy({[1, 1], [1, 2]})
        ).is(
            false
        );
};
nestedForall();

// ************************************************************************** \\

printTestResult();

